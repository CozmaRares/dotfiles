complete_flags() {
    local -a flags
    flags=(
        "--help:show help message"
        "--layout:create a new layout or edit existing one"
        "--ls:show running sessions and layouts"
        "--preview:preview session or layout"
        "--picker:pick session or layout"
    )

    _describe 'flags' flags
}

complete_aliases() {
    local -a aliases
    aliases=(
       "-h:alias for --help"
       "help:alias for --help"
       "-l:alias for --layout"
       "ls:alias for --ls"
    )

    _describe 'aliases' aliases
}

complete_sessions() {
    local -a sessions
    sessions=$(tmux ls 2>/dev/null)

    if [ $? -eq 0 ]; then
        sessions=("${(@f)$(echo "$sessions" | awk -F '[:,()]' '{printf "%s:%d windows\n", $1, $2}')}")
        _describe 'sessions' sessions
    fi
}

list_layouts() {
    find "$TX_ROOT/layouts" -type f -name '*.layout.sh' -exec basename {} .layout.sh \;
}

complete_layouts() {
    local -a sessions layouts
    sessions=($(tmux ls 2>/dev/null | awk -F '[:,()]' '{print $1}'))
    layouts=()

    while read -r layout; do
        if ! echo "${sessions[@]}" | grep -qx "$layout"; then
            layouts+=("$layout")
        fi
    done < <(list_layouts)

    _describe "layouts" layouts
}

complete_layouts_edit() {
    local -a layouts
    layouts=("${(@f)$(list_layouts)}")
    _describe 'layouts' layouts
}

_tx() {
    if (( CURRENT == 3 )); then
        case "${words[2]}" in
            -l | --layout)
                complete_layouts_edit
                ;;
        esac
    else
        complete_sessions
        complete_layouts
        complete_flags
        complete_aliases
    fi
}

compdef _tx tx
